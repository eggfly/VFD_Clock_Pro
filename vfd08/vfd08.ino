
//arduino uno r3 引脚接法

uint8_t din = 11;  // DA
uint8_t clk = 13;  // CK
uint8_t cs = 10;   // CS
uint8_t Reset = 1; // RS
uint8_t en = 0;    // EN

void write_6302(unsigned char w_data)
{
  unsigned char i;
  for (i = 0; i < 8; i++) {
    digitalWrite(clk, LOW);

    if ((w_data & 0x01) == 0x01)
      digitalWrite(din, HIGH);
    else
      digitalWrite(din, LOW);

    w_data >>= 1;
    digitalWrite(clk, HIGH);
  }
}

void VFD_cmd(unsigned char command)
{
  digitalWrite(cs, LOW);
  write_6302(command);
  digitalWrite(cs, HIGH);
  delayMicroseconds(5);
}

void S1201_show(void)
{
  digitalWrite(cs, LOW);  //开始传输
  write_6302(0xe8);       //地址寄存器起始位置
  digitalWrite(cs, HIGH); //停止传输
}

void VFD_init()
{
  //SET HOW MANY digtal numbers
  digitalWrite(cs, LOW);
  write_6302(0xe0);
  delayMicroseconds(5);
  write_6302(0x07); //8 digtal
  digitalWrite(cs, HIGH);
  delayMicroseconds(5);

  //set bright
  digitalWrite(cs, LOW);
  write_6302(0xe4);
  delayMicroseconds(5);
  write_6302(0xff); //leve 255 max
  digitalWrite(cs, HIGH);
  delayMicroseconds(5);
}

/******************************
  在指定位置打印一个字符(用户自定义,所有CG-ROM中的)
  x:0~11;chr:要显示的字符编码
*******************************/
void S1201_WriteOneChar(unsigned char x, unsigned char chr)
{
  digitalWrite(cs, LOW); //开始传输
  write_6302(0x20 + x);  //地址寄存器起始位置
  write_6302(chr + 0x30);
  digitalWrite(cs, HIGH); //停止传输
  S1201_show();
}

/******************************
  在指定位置打印字符串
  (仅适用于英文,标点,数字)
  x:0~11;str:要显示的字符串
*******************************/
void S1201_WriteStr(unsigned char x, const char *str)
{
  digitalWrite(cs, LOW); //开始传输
  write_6302(0x20 + x);  //地址寄存器起始位置
  while (*str) {
    write_6302(*str); //ascii与对应字符表转换
    str++;
  }
  digitalWrite(cs, HIGH); //停止传输
  S1201_show();
}

void setup()
{
  pinMode(en, OUTPUT);
  pinMode(clk, OUTPUT);
  pinMode(din, OUTPUT);
  pinMode(cs, OUTPUT);
  pinMode(Reset, OUTPUT);
  digitalWrite(en, HIGH);
  digitalWrite(Reset, LOW);
  delayMicroseconds(5);
  digitalWrite(Reset, HIGH);
  VFD_init();
}

void loop()
{
  VFD_cmd(0xE9); // 全亮
  delay(1000);
  S1201_WriteOneChar(0, 0);
  S1201_WriteOneChar(1, 1);
  S1201_WriteOneChar(2, 2);
  S1201_WriteOneChar(3, 3);
  S1201_WriteOneChar(4, 4);
  S1201_WriteOneChar(5, 5);
  S1201_WriteOneChar(6, 6);
  S1201_WriteOneChar(7, 7);
  delay(1000);
  S1201_WriteStr(0, "ABCDEFGH");
  delay(1000);
}
